CREATE OR ALTER PROCEDURE SchemaName.DropTableConstraints
AS
/***************************************************************************************************
----------------------------------------------------------------------------------------------------
Procedure:      DropTableConstraints
Create Date:    2021-01-01 
Author:         Sorob Cyrus
Description:    Drops all Table Constraints  
Call by:        TBD, UI, Add hoc
Steps:          NA
Parameter(s):   None
Usage:          EXEC SchemaName.DropTableConstraints
****************************************************************************************************
SUMMARY OF CHANGES
Date(yyyy-mm-dd)    Author              Comments
------------------- ------------------- ------------------------------------------------------------
****************************************************************************************************/
SET NOCOUNT ON;

DECLARE @ErrorText VARCHAR(MAX),      
        @Message   VARCHAR(255),   
        @StartTime DATETIME,
        @SP        VARCHAR(50),
        @SQL       NVARCHAR(MAX);

BEGIN TRY;   
SET @ErrorText = 'Unexpected ERROR in setting the variables!';

SET @SQL = SPACE(0);
SET @SP = OBJECT_NAME(@@PROCID);
SET @StartTime = GETDATE();    
SET @Message = 'Started SP ' + @SP + ' at ' + FORMAT(@StartTime , 'MM/dd/yyyy HH:mm:ss');   
RAISERROR (@Message, 0,1) WITH NOWAIT;

-------------------------------------------------------------------------------
SET @ErrorText = 'Failed DROP TABLE CONSTRAINTS.';

SELECT @SQL = @SQL + N'
  ALTER TABLE ' + QUOTENAME(s.name) + N'.'
  + QUOTENAME(t.name) + N' DROP CONSTRAINT '
  + QUOTENAME(c.name) + ';'
FROM sys.objects AS c
  INNER JOIN sys.tables AS t
  ON c.parent_object_id = t.[object_id]
  INNER JOIN sys.schemas AS s
  ON t.[schema_id] = s.[schema_id]
WHERE c.[type] IN ('D','C','F','PK','UQ') AND s.name = 'SchemaName'
ORDER BY c.[type];

RAISERROR(@SQL, 0,1) WITH NOWAIT;
EXEC sys.sp_executesql @SQL;

SET @Message = 'Completed DROP TABLE CONSTRAINTS.';
RAISERROR(@Message, 0,1) WITH NOWAIT;

-------------------------------------------------------------------------------
SET @Message = 'Completed SP ' + @SP + '. Duration in minutes:  '   
   + CONVERT(VARCHAR(12), CONVERT(DECIMAL(6,2),datediff(mi, @StartTime, getdate())));    
RAISERROR(@Message, 0,1) WITH NOWAIT;

END TRY

BEGIN CATCH;
	IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;

	SET @ErrorText = 'Error: '+CONVERT(VARCHAR,ISNULL(ERROR_NUMBER(),'NULL'))      
					  +', Severity = '+CONVERT(VARCHAR,ISNULL(ERROR_SEVERITY(),'NULL'))      
					  +', State = '+CONVERT(VARCHAR,ISNULL(ERROR_STATE(),'NULL'))      
					  +', Line = '+CONVERT(VARCHAR,ISNULL(ERROR_LINE(),'NULL'))      
					  +', Procedure = '+CONVERT(VARCHAR,ISNULL(ERROR_PROCEDURE(),'NULL'))      
					  +', Server Error Message = '+CONVERT(VARCHAR(100),ISNULL(ERROR_MESSAGE(),'NULL'))      
					  +', SP Defined Error Text = '+@ErrorText;

	THROW 66000, @ErrorText, 1; 

END CATCH;      

